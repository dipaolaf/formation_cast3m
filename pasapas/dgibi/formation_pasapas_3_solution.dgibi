* fichier : formation_pasapas_3_solution.dgibi
*
*----------------------------------------------------------------------*
*              FORMATION AVANCEE SUR LA PROCEDURE PASAPAS              *
*                                                                      *
* Chauffage d'une plaque par une source de chaleur variable            *
* dependante de la temperature                                         *
*                                                                      *
* Ce fichier constitue la mise donnee solution du probleme et realise  *
* - le maillage                                                        *
* - la definition du modele et des parametres materiaux variables      *
* - la definition du chargement (convection + source variable)         *
* - la resolution avec PASAPAS                                         *
* - le post traitement                                                 *
*----------------------------------------------------------------------*





** Options de trace / Plotting options
itrac = VRAI ;
OPTI 'TRAC' 'PSC' ;
ipsc  = EGA (VALE 'TRAC') 'PSC' ;

** Options generales
OPTI 'DIME' 2 'ELEM' 'QUA8' ;

** Maillage
p1    = 0. 0. ;
p2    = 1. 0. ;
l12   = DROI 20 p1 p2 ;
mail  = l12 TRAN 20 (0. 1.) ;
ptx   = (mail COOR 1) POIN 'EGINFE' 0.25001 ;
pty   = (ptx  COOR 2) POIN 'EGINFE' 0.25001 ;
sou   = mail ELEM 'APPU' 'STRI' pty ;
nsou  = DIFF mail sou ;
lext  = (mail COTE 2) ET (mail COTE 3) ;
p2    = mail 'POIN' 'PROC' (1. 1.) ;
SI itrac ;
  SI ipsc ;
***    OPTI 'FTRA' 'exo_3_maillage.ps' ;
  FINSI ;
***  TRAC 'FACE' ((sou COUL 'ORAN') ET (nsou COUL 'CYAN')) ((CONT sou) ET (CONT nsou))  ;
FINSI ;

** Modeles et materiaux
mocod = MODE mail 'THERMIQUE' 'CONDUCTION' ;
lt    = PROG 0. 1.E6 ;
lk    = (0.3 * lt) + 200. ;
evlam = EVOL 'MANU' 'T' lt 'K' lk ;
macod = MATE mocod 'K' evlam 'C' 128.
                   'RHO' 19300. 'TINI' 20. ;
* convection
mocov = MODE lext 'THERMIQUE' 'CONVECTION' ;
ltps  = PROG 0. 1.E5 ;
lh    = PROG 10. 200. ;
evh   = EVOL 'MANU' 'TEMP' ltps 'H' lh ;
macov = MATE mocov 'H' evh 'TC' 20. ;

** Chargement (source)
mosou = REDU mocod sou ;
psou  = SOUR mosou 4.E6 sou ;
chsou = CHAR 'Q' psou ;

** Procedure PERSO2
DEBP PERSO2 t1*'TABLE' ;
  masou = t1 . 'MON_MAILLAGE' ;
  mosou = t1 . 'MON_MODELE' ;
* reduction temperatures sur la source
  tp1   = t1 . 'WTABLE' . 'THER_COURANT' ;
  tsou  = REDU tp1 masou ;
* calcul du champ de source selon temperature
  ex1   = ((tsou - 1000.) / 700.) ** 2 ;
  chs   = 4.E6 * (EXP (-1. * ex1)) ;
* calcul du du second membre et chargement
  pmaj  = SOUR mosou chs ;
  chmaj = CHAR 'Q' pmaj ;
* modification du chargement dans la table
  t1 . 'WTABLE' . 'CHARGEMENT' = chmaj ;
FINP ;

** Procedure CHARTHER
DEBP CHARTHER t1*'TABLE' tps1*'FLOTTANT' ;
  masou = t1 . 'MON_MAILLAGE' ;
  mosou = t1 . 'MON_MODELE' ;
* reduction temperatures sur la source
  tp1   = t1 . 'WTABLE' . 'THER_COURANT' ;
  tsou  = REDU tp1 masou ;
* calcul du champ de source selon temperature
  ex1   = ((tsou - 1000.) / 700.) ** 2 ;
  chs   = 4.E6 * (EXP (-1. * ex1)) ;
* calcul du second membre
  pmaj  = SOUR mosou chs ;
* sortie du second membre pour ajout
  t2                 = TABL ;
  t2 . 'ADDI_SECOND' = pmaj ;
FINP t2 ;

**Resolution avec PASAPAS
tab1                      = TABL ;
tab1 . 'MODELE'           = mocod ET mocov ;
tab1 . 'CARACTERISTIQUES' = macod ET macov ;
tab1 . 'CHARGEMENT'       = chsou ;
tab1 . 'TEMPS_CALCULES'   = PROG 0. 'PAS' (1.E5 / 50.) 1.E5 ;
tab1 . 'PROCEDURE_PERSO2' = VRAI ;
tab1 . 'MON_MAILLAGE'     = sou ;
TAB1 . 'MON_MODELE'       = mosou ;
PASAPAS tab1 ;

** Post-traitement
n1    = DIME (tab1 . 'TEMPS_CALCULES') ;
liso  = PROG 20. 'PAS' ((1800. - 20.) / 20.) 1800. ;
SI ipsc ;
  OPTI 'FTRA' 'exo_3_solu_temperature.ps' ;
FINSI ;
REPE b1 n1 ;
  tps1  = tab1 . 'TEMPS' . (&b1 - 1) ;
  p100  = ENTI (100. * tps1 / 1.E5) ;
  te1   = tab1 . 'TEMPERATURES' . (&b1 - 1) ;
  SI itrac ;
    TRAC te1 mail (CONT mail) liso 'TITR' (CHAI 'Temperature au temps' tps1 ' (' p100 '%)') ;
  FINSI ;
FIN b1 ;
ev1   = EVOL 'ROUG' 'TEMP' tab1 'TEMPERATURES' 'T' p1 ;
ev2   = EVOL 'BLEU' 'TEMP' tab1 'TEMPERATURES' 'T' p2 ;
tl    = TABL ;
tl . 'TITRE' = TABL ;
tl . 'TITRE' . 1 = 'Coin inf. gauche' ;
tl . 'TITRE' . 2 = 'Coin sup. droite' ;
SI itrac ;
  SI ipsc ;
    OPTI 'FTRA' 'exo_3_solu_evol.ps' ;
  FINSI ;
  DESS (ev1 ET ev2) 'LEGE' 'SE' tl 'TITR' 'Temperature vs Temps' 'YBOR' 0. 2000. ;
FINSI ;

FIN ;








***tab1 . 'CHARGEMENT'       = chsou ;

tab1 . 'PROCEDURE_CHARTHER' = VRAI ;
tab1 . 'MON_MAILLAGE'     = sou ;
tab1 . 'MON_MODELE'       = mosou ;
PASAPAS tab1 ;
