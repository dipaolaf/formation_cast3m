* fichier : formation_pasapas_3_solution.dgibi
*
*----------------------------------------------------------------------*
*              FORMATION AVANCEE SUR LA PROCEDURE PASAPAS              *
*                                                                      *
* Chauffage d'une plaque par une source de chaleur                     *
* dependante de la temperature                                         *
*                                                                      *
* Ce fichier constitue la mise donnee solution du probleme et realise  *
* - le maillage                                                        *
* - la definition du modele et des parametres materiaux                *
* - la definition du chargement (convection + source variable)         *
* - la resolution avec PASAPAS                                         *
* - le post traitement                                                 *
*----------------------------------------------------------------------*





** Options de trace / Plotting options
itrac = FAUX ;
***OPTI 'TRAC' 'PSC' ;
ipsc  = EGA (VALE 'TRAC') 'PSC' ;

** Options generales / General options
OPTI 'DIME' 3 'ELEM' 'CUB8' 'DENS' 0.0015 ;

** Maillage / Mesh
l1    = 0.05 ;
l2    = 0.02 ;
e1    = 0.002 ;
l3    = (l1 - l2) / 2. ;
lig1  = DROI (0. 0. 0.) (l3 0. 0.) DROI ((l1 - l3) 0. 0.) DROI (l1 0. 0.) ;
sur1  = lig1 TRAN (0. l3 0.) TRAN (0. l2 0.) TRAN (0. l3 0.) ;
sub   = sur1 VOLU 2 'TRAN' (0. 0. e1) COUL 'GRIS' ;
sur2  = sub FACE 2 ;
x     = COOR 1 sur2 ;
ptx   = x POIN 'COMPRIS' (0.5 * (l1 - l2)) (0.5 * (l1 + l2)) ;
y     = COOR 2 ptx ;
pty   = y POIN 'COMPRIS' (0.5 * (l1 - l2)) (0.5 * (l1 + l2)) ;
sur3  = sur2 ELEM 'APPUYE' 'STRICTEMENT' pty ;
sur2  = DIFF sur2 sur3 ;
sou   = sur3 VOLU 2 'TRAN' (0. 0. e1) COUL 'ROUG' ;
sur4  = sou FACE 2 ;
msh   = sub ET sou ;
are   = ARET msh COUL 'DEFA' ;
SI ipsc ;
  OPTI 'FTRA' 'exo_3_maillage.ps' ;
FINSI ;
SI itrac ;
  TRAC 'ARET' msh are 'FACE' ;
FINSI ;

** Modele et materiau / Model and material properties
mosub = MODE sub 'THERMIQUE' 'CONDUCTION' ;
masub = MATE mosub 'K' 0.32 'C' 962. 'RHO' 1900. 'TINI' 25. ;
mosou = MODE sou 'THERMIQUE' 'CONDUCTION' ;
masou = MATE mosou 'K' 36. 'C' 800. 'RHO' 3300. 'TINI' 25. ;
mohsu = MODE (sur1 ET sur2) 'THERMIQUE' 'CONVECTION' ;
mahsu = MATE mohsu 'H' 17.26 'TC' 25. ;
mohso = MODE sur4 'THERMIQUE' 'CONVECTION' ;
mahso = MATE mohso 'H' 27.3 'TC' 25. ;

** Chargement / Load (source = 1W)
q0    = 1. / (MESU sou) ;
chq0  = MANU 'CHPO' sou 'Q' q0 ;
qsou  = SOUR mosou chq0 ;
chsou = CHAR 'Q' qsou ;

** Evolution source vs temperature
lte   = PROG 0. 'PAS' 10. 1000. ;
ea    = 0.15 * 1.6E-19 ;
kb    = 1.38E-23 ;
lq    = q0 * (EXP (-1. * ea / (kb * (lte + 273.)))) /
             (EXP (-1. * ea / (kb * (25. + 273.)))) ;
evq   = EVOL 'VERT' 'MANU' 'T' lte 'Q' lq ;
SI ipsc ;
  OPTI 'FTRA' 'exo_3_arrhenius.ps' ;
FINSI ;
SI itrac ;
  DESS (evq / q0) 'GRIL' 'TIRC' 'GRIS' 'TITX' 'T (degres C)' 'TITY' 'Q(T)/Q0' ;
FINSI ;

** Procedure CHARTHER
DEBP CHARTHER t1*'TABLE' tps1*'FLOTTANT' ;
  te    = t1 . 'WTABLE' . 'THER_COURANT' ;
  tesou = REDU te sou ;
  chq   = IPOL tesou evq ;
  qsou  = SOUR mosou chq ;
  t2    = TABL ;
  t2 . 'ADDI_SECOND' = qsou ;
FINP t2 ;

** Resolution
tab0                      = TABL ;
tab0 . 'MODELE'           = mosub ET mosou ET mohsu ET mohso ;
tab0 . 'CARACTERISTIQUES' = masub ET masou ET mahsu ET mahso ;
tab0 . 'CHARGEMENT'       = chsou ;
tab0 . 'TEMPS_CALCULES'   = PROG 0. 'PAS' 30. 600. ;
PASAPAS tab0 ;

** Resolution
tab1                      = TABL ;
tab1 . 'MODELE'           = mosub ET mosou ET mohsu ET mohso ;
tab1 . 'CARACTERISTIQUES' = masub ET masou ET mahsu ET mahso ;
tab1 . 'TEMPS_CALCULES'   = PROG 0. 'PAS' 10. 600. ;
***tab1 . 'CHARGEMENT'       = chsou ;
tab1 . 'PROCEDURE_CHARTHER' = VRAI ;
PASAPAS tab1 ;

** Post-traitement / Post-processing
liso  = PROG 20. 'PAS' 20. 600. ;
SI ipsc ;
  OPTI 'FTRA' 'exo_3_solu_temperature.ps' ;
FINSI ;
REPE b1 (DIME (tab1 . 'TEMPS')) ;
  tps1  = tab1 . 'TEMPS' . (&b1 - 1) ;
  p100  = ENTI (100. * tps1 / 600.) ;
  te1   = tab1 . 'TEMPERATURES' . (&b1 - 1) ;
  SI itrac ;
    TRAC te1 msh are liso 'TITR' (CHAI 'Temperature au temps' ' ' (ENTI tps1) ' (' p100 '%)') ;
  FINSI ;
FIN b1 ;
SI ipsc ;
  OPTI 'FTRA' 'exo_3_solu_evol.ps' ;
FINSI ;
pt1   = sou POIN 'PROC' (0. 0. 0.003) ;
ev0   = EVOL 'BLEU' 'TEMP' tab0 'TEMPERATURES' 'T' pt1 ;
ev1   = EVOL 'ROUG' 'TEMP' tab1 'TEMPERATURES' 'T' pt1 ;
SI itrac ;
  DESS (ev0 ET ev1) 'GRIL' 'TIRC' 'GRIS' 'XBOR' 0. 600. 'XGRA' 100. 'YBOR' 0. 300. 'YGRA' 30. ;
FINSI ;

FIN ;

