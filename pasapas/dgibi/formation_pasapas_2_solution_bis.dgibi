* fichier : formation_pasapas_2_initial.dgibi
*
*----------------------------------------------------------------------*
*              FORMATION AVANCEE SUR LA PROCEDURE PASAPAS              *
*                                                                      *
* Rupture d'une plaque trouee en traction                              *
* Comportement elastique lineaire                                      *
*                                                                      *
* Ce fichier constitue la mise donnee solution du probleme et realise  *
* - le maillage                                                        *
* - la definition du modele et des parametres materiaux                *
* - la definition du chargement                                        *
* - la resolution avec PASAPAS                                         *
* - la suppression des blocages selon un critere de rupture            *
*   base sur la 1ere contrainte principale (avec la procedure PERSO1)  *
* - le post traitement (contraintes, courbe force/deplacement)         *
*----------------------------------------------------------------------*





** Options de trace / Plotting options
itrac = vrai ;
OPTI 'TRAC' 'PSC' ;
ipsc  = EGA (VALE 'TRAC') 'PSC' ;

** Options generales / General options
OPTI 'DIME' 2 'ELEM' 'QUA8' 'MODE' 'PLAN' 'DEFO' ;

** Parametres geometriques / Geometrical parameters
l1    = 1. ;
r1    = 0.15 ;

** Maillage / Mesh
dens1 = R1 / 15. ;
dens2 = l1 / 15. ;
p0    = 0. 0. ;
p1    = r1 0. ;
p2    = 0. r1 ;
p3    = l1 0. ;
p4    = l1 l1 ;
p5    = 0. l1 ;
CER   = CERC 20 p1 p0 p2 ;
lid   = DROI 10 p3 p4 ;
lih   = DROI 10 p4 p5 ;
s1    = REGL cer (lid ET lih) 'DINI' dens1 'DFIN' dens2 ;
lig   = s1 COTE 2 ;
lib   = s1 COTE 4 ;
cs1   = CONT s1 ;

** Modele et materiau / Model and material properties
mo    = MODE s1 'MECANIQUE' 'ELASTIQUE' ;
ma    = MATE mo 'YOUN' 200.E9 'NU' 0.3 ;

** Blocages mecaniques
bl1   = BLOQ 'UY' lib ;
bl2   = BLOQ 'UX' lig ;
bl3   = BLOQ 'UY' lih ;
bl    = bl1 ET bl2 ET bl3 ;

** Chargement en deplacement impose / Load with imposed displacement
umax  = l1 / 20. ;
f1    = DEPI bl3 umax ;
ev1   = EVOL 'MANU' (PROG 0. 1.) (PROG 0. 1.) ;
cha   = CHAR 'DIMP' f1 ev1 ;
SI ipsc ;
***  OPTI 'FTRA' 'exo_2_chargement.ps' ;
FINSI ;
SI itrac ;
***  TRAC (VECT f1 ' ' 'FLX' 'VERT') ((RIGI mo ma) ET bl1 ET bl2) ;
FINSI ;

** Procedure PERSO1
DEBP PERSO1 t1*'TABLE' ;
  mo1   = t1 . 'WTABLE' . 'MODELE' ;
  mail1 = EXTR mo1 'MAIL' ;
  sg1   = t1 . 'ESTIMATION' . 'CONTRAINTES' ;
  sgp1  = PRIN sg1 mo1 ;
  sgi   = EXCO 'SI11' sgp1 ;
  sg11  = CHAN 'GRAVITE' sgi mo1 ;
  mail2 = sg11 ELEM 'INFERIEUR' 2.2E10 ;
  mail3 = DIFF mail1 mail2 ;
  SI ((NBEL mail3) > 0) ;
    bl1 = t1 . 'WTABLE' . 'BLOCAGES_MECANIQUES' ;
    mb1 = EXTR bl1 'MAIL' ;
    mb2 = mb1 ELEM 'APPU' 'LARG' mail3 ;
    mb3 = DIFF mb1 mb2 ;
    bl2  = REDU bl1 mb3 ;
    t1 . 'WTABLE' . 'BLOCAGES_MECANIQUES' = bl2 ;
  FINSI ;
FINP ;

** Resolution
tab1                         = TABL ;
tab1 . 'MODELE'              = mo ;
tab1 . 'CARACTERISTIQUES'    = ma ;
tab1 . 'BLOCAGES_MECANIQUES' = bl ;
tab1 . 'CHARGEMENT'          = cha ;
tab1 . 'TEMPS_CALCULES' =
  PROG 0.1 'PAS' 0.1 0.6 'PAS' 0.01 1. ;
tab1 . 'PROCEDURE_PERSO1'    = VRAI ;
tab1 . 'GRANDS_DEPLACEMENTS' = VRAI ;
PASAPAS tab1 ;

** Post-traitement
liso  = PROG 0. 'PAS' 4.E9 4.E10 ;
box   = (0. -0.3) ET (1. 1.4) ;
nt1   = DIME (tab1 . 'TEMPS') ;
SI ipsc ;
  OPTI 'FTRA' 'exo_2_solu_bis_sigma.ps' ;
FINSI ;
REPE b1 nt1 ;
* contraintes / stresses
  sg1   = tab1 . 'CONTRAINTES' . (&b1 - 1) ;
  sp1   = EXCO 'SI11' (PRIN sg1 mo) ;
* deplacements / displacements
  u1    = tab1 . 'DEPLACEMENTS' . (&b1 - 1) ;
* reactions
  r1    = tab1 . 'REACTIONS' . (&b1 - 1) ;
  vec1  = VECT r1 5.E-10 'FORC' 'VIOL' ;
* deformees / deformed shape
  def1  = DEFO s1 u1 2. vec1 sp1 mo ;
* traces / plot
  SI itrac ;
    TRAC def1 cs1 liso 'BOIT' box ;
  FINSI ;
FIN b1 ;
* courbe force vs deplacement / force vs displacement curve
evu   = EVOL 'TEMP' tab1 'DEPLACEMENTS' 'UY' p5 ;
evf   = -1. * (@GLOBAL tab1 bl1 evu 'FY') ;
evfu  = EVOL 'ROUG' 'MANU' 'Deplac. UY'  (EXTR evu 'ORDO')
                           'Reaction FY' (EXTR evf 'ORDO') ;
SI ipsc ;
  OPTI 'FTRA' 'exo_2_solu_bis_evol.ps' ;
FINSI ;
SI itrac ;
  DESS evfu ;
FINSI ;


FIN ;
