* fichier : formation_pasapas_2_initial.dgibi
*
*----------------------------------------------------------------------*
*              FORMATION AVANCEE SUR LA PROCEDURE PASAPAS              *
*                                                                      *
* Rupture d'une eprouvette entaillee en traction                       *
* Comportement elastique lineaire                                      *
*                                                                      *
* Ce fichier constitue la mise donnee solution du probleme et realise  *
* - le maillage                                                        *
* - la definition du modele et des parametres materiaux                *
* - la definition du chargement                                        *
* - la resolution avec PASAPAS                                         *
* - la suppression d'elements du maillage selon un critere de rupture  *
*   base sur la 1ere contrainte principale (avec la procedure PERSO1)  *
* - le post traitement (contraintes, courbe force/deplacement)         *
*----------------------------------------------------------------------*





** Options de trace / Plotting options
itrac = FAUX ;
***OPTI 'TRAC' 'PSC' ;
ipsc  = EGA (VALE 'TRAC') 'PSC' ;

** Options generales / General options
OPTI 'DIME' 2 'ELEM' 'TRI6' 'MODE' 'PLAN' 'CONT' ;

** Maillage / Mesh
OPTI 'DENS' 0.1 ;
p1   = 0.   0. ;
p2   = 1. 0. ;
p3   = 1.   1. ;
p4   = 0.55 1. ;
p7   = 0.45 1. ;
p8   = 0. 1. ;
OPTI 'DENS' 0.005 ;
pc   = 0.5 0.5 ;
p5   = pc PLUS ( 0.005 -0.0025) ;
p6   = pc PLUS (-0.005 -0.0025) ;
con  = DROI p1 p2 DROI p3 DROI p4 DROI p5 CERC pc p6 DROI p7 DROI p8 DROI p1 ;
s1   = SURF con ;
l1   = con ELEM 'COMP' p8 p1 ;
l2   = con ELEM 'COMP' p2 p3 ;

** Modele et materiau / Model and material properties
mo   = MODE s1 'MECANIQUE' ;
ma   = MATE mo 'YOUN' 210.E9 'NU' 0.3 ;

** Blocages mecaniques / mechanical blocking
bl1   = BLOQ 'UX' l1 ;
bl2   = BLOQ 'UX' l2 ;
bl3   = BLOQ 'UY' (p1 ET p2) ;
bl    = bl1 ET bl2 ET bl3 ;

** Chargement en deplacement impose / Load with imposed displacement
u2    = DEPI bl2 0.025 ;
ev1   = EVOL 'MANU' (PROG 0. 1.) (PROG 0. 1.) ;
cha   = CHAR 'DIMP' u2 ev1 ;
SI ipsc ;
  OPTI 'FTRA' 'exo_2_chargement.ps' ;
FINSI ;
SI itrac ;
  TRAC (VECT u2 'FLX' ' ' 'VERT') ((RIGI mo ma) ET bl1 ET bl3) ;
FINSI ;

** Procedure PERSO1
DEBP PERSO1 t1*'TABLE' ;
  mo1   = t1 . 'WTABLE' . 'MODELE' ;
  sig   = t1 . 'ESTIMATION' . 'CONTRAINTES' ;
  sigp  = PRIN sig mo1 ;
  sigp1 = EXCO 'SI11' sigp ;
  sigp1 = CHAN 'GRAVITE' sigp1 mo1 ;
  s2    = sigp1 ELEM 'INFERIEUR' 10.E9 ;
  mo2   = REDU mo1 s2 ;
  t1 . 'WTABLE' . 'MODELE' = mo2 ;
FINP ;

** Resolution
tab1                         = TABL ;
tab1 . 'MODELE'              = mo ;
tab1 . 'CARACTERISTIQUES'    = ma ;
tab1 . 'BLOCAGES_MECANIQUES' = bl ;
tab1 . 'CHARGEMENT'          = cha ;
tab1 . 'TEMPS_CALCULES'      = PROG 0.1 'PAS' 0.05 1. ;
tab1 . 'PROCEDURE_PERSO1'    = VRAI ;
PASAPAS tab1 ;

** Post-traitement / Post-processing
liso  = PROG 0. 'PAS' 0.5E9 20.E9 ;
*box   = (0. -0.2) ET (1. 1.4) ;
nt1   = DIME (tab1 . 'TEMPS') ;
SI ipsc ;
  OPTI 'FTRA' 'exo_2_solu_sigma.ps' ;
FINSI ;
REPE b1 nt1 ;
* contraintes / stresses
  sg1   = tab1 . 'CONTRAINTES' . (&b1 - 1) ;
* modele / model
  msh1  = EXTR sg1 'MAIL' ;
  mo1   = REDU mo msh1 ;
* contraintes principales / principal stresses
  sp1   = EXCO 'SI11' (PRIN sg1 mo1) ;
* deplacements / displacements
  u1    = tab1 . 'DEPLACEMENTS' . (&b1 - 1) ;
* deformee / deformed shape
  def1  = DEFO s1 u1 5. ;
* traces / plot
  SI itrac ;
    TRAC sp1 mo1 def1 con liso ;
  FINSI ;
FIN b1 ;

FIN ;

