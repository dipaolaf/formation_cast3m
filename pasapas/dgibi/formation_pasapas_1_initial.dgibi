* fichier : formation_pasapas_1_initial.dgibi
*
*----------------------------------------------------------------------*
*              FORMATION AVANCEE SUR LA PROCEDURE PASAPAS              *
*                                                                      *
* Flexion d'une poutre en grands deplacements avec chargemet suiveur   *
*                                                                      *
* Ce fichier constitue la mise donnee initiale du probleme et realise  *
* - le maillage                                                        *
* - la definition du modele et des parametres materiaux                *
* - la definition du chargement sur la configuration initiale          *
* - la resolution avec PASAPAS en hypothese grands deplacements        *
* - le post traitement du deplacement de la poutre                     *
*----------------------------------------------------------------------*





** Options de trace / Plotting options
itrac = FAUX ;
***OPTI 'TRAC' 'PSC' ;
ipsc = EGA (VALE 'TRAC') 'PSC' ;

** Options generales / General options
OPTI 'DIME' 2 'ELEM' 'SEG2' ;

** Maillage / Mesh
p1   = 0. 0. ;
p2   = 0. 1. ;
l1   = DROI 25 p1 p2 ;

** Modele et materiau / Model and material properties
mo1  = MODE l1 'MECANIQUE' 'ELASTIQUE' 'POUT' ;
ma1  = MATE mo1 'YOUN' 50.E6 'NU' 0.3 'SECT' 1.E-2 'INRZ' 1.E-4 ;

** Blocages de la base / Clamping the base
blo1 = BLOQ 'DEPL' 'ROTA' p1 ;

** Chargement en effort impose / Load with imposed force
f1   = FORC (1. 0.) p2 ;
ev1  = EVOL 'MANU' 'Temps' (PROG 0. 1.) 'Coef' (PROG 0. 2.E4) ;
cha1 = CHAR 'MECA' f1 ev1 ;

** Resolution
tab1                         = TABL ;
tab1 . 'MODELE'              = mo1 ;
tab1 . 'CARACTERISTIQUES'    = ma1 ;
tab1 . 'BLOCAGES_MECANIQUES' = blo1 ;
tab1 . 'CHARGEMENT'          = cha1 ;
tab1 . 'TEMPS_CALCULES'      = PROG 0. 'PAS' 0.05 1. ;
tab1 . 'GRANDS_DEPLACEMENTS' = VRAI ;
PASAPAS tab1 ;

** Post-traitement / Post-processing
tt   = tab1 . 'TEMPS' ;
tu1  = tab1 . 'DEPLACEMENTS' ;
tr1  = tab1 . 'REACTIONS' ;
def0 = DEFO l1 (tu1 . 0) 0. ;
* trace des deformees / plotting deformed shape
SI ipsc ;
  OPTI 'FTRA' 'exo_1_deformee.ps' ;
FINSI ;
box  = (-0.5 -0.1) ET (0.85 1.) COUL 'JAUN' ;
REPE b1 (DIME tt) ;
  i1   = &b1 - 1 ;
  tps1 = tt . i1 ;
  u1   = tu1 . i1 ;
  r1   = tr1 . i1 ;
  v1   = VECT r1 2.E-5 'FORC' 'ROUG' ;
  def1 = DEFO (l1 ET box) u1 1. v1 'BLEU' ;
  SI itrac ;
    TRAC (def0 ET def1) 'TITR' (CHAI 'Deformee au temps ' tps1) ;
  FINSI ;
FIN b1 ;
* evolution temporelle de la rotation / time evolution of rotation
evrz = EVOL 'ROUG' 'TEMP' tab1 'DEPLACEMENTS' 'RZ' p2 ;
SI ipsc ;
  OPTI 'FTRA' 'exo_1_evol.ps' ;
FINSI ;
SI itrac ;
  DESS evrz 'TITR' 'Rotation en p2 vs Temps' 'TITY' 'ROTATION (RAD)' ;
FINSI ;

FIN ;

