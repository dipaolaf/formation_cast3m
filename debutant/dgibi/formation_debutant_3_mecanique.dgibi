* fichier : formation_debutant_3_mecanique.dgibi
*
*----------------------------------------------------------------------*
*      FORMATION DEBUTER AVEC CAST3M - CALCULS THERMO-MECANIQUES       *
*                                                                      *
* Modelisation du comportement thermo-mecanique d'une piece trouee     *
*                                                                      *
* Ce fichier est la partie 3 sur 3 de la formation debutant a Cast3M   *
*                                                                      *
* Partie 3 : calculs mecaniques                                        *
*            restitution des objets                                    *
*            recuperation des zones maillees                           *
*            elasticite lineaire, plasticite parfaite                  *
*            chargement thermique                                      *
*            caracteristiques variables                                *
*                                                                      *
*----------------------------------------------------------------------*




*----------------------------------------------------------------------*
*             R E S T I T U T I O N   D E S   O B J E T S              *
*                                                                      *
*              R E S T O R I N G   T H E   O B J E C T S               *
*----------------------------------------------------------------------*

OPTI 'REST' 'formation_debutant_2_thermique.sauv' ;
REST ;
itrac = vrai ;
OPTI 'TRAC' 'PSC' ;
ipsc = EGA (VALE 'TRAC') 'PSC' ;



*----------------------------------------------------------------------*
*                P A R M E T R E S   D E   C A L C U L                 *
*                                                                      *
*              C A L C U L A T I O N   P A R M E T E R S               *
*----------------------------------------------------------------------*

** Parametres materiau / material parameters
you    = 200.E9 ;
nu     = 0.3 ;
alpha  = 1.E-5 ;
sigy   = 250.E6 ;

** Parametres des conditions limites et chargement /
*  Parameters for boundary conditions and loading
mass1  = 2500. ;



*----------------------------------------------------------------------*
* Chapitre / Chapter    6                                              *
*----------------------------------------------------------------------*
*                   C A L C U L   M E C A N I Q U E                    *
*                              elasticite                              *
*              deplacements + forces surfaciques imposes               *
*             lineaire, quasi statique, materiau homogene              *
*                                                                      *
*               M E C H A N I C H A L   A N A L Y S I S                *
*                              elasticity                              *
*                imposed displacements + surface forces                *
*              linear, quasi static, homogeneous material              *
*----------------------------------------------------------------------*

** Modele mecanique elastique / Elastic mechanichal model
mom1 = MODE vol1 'MECANIQUE' 'ELASTIQUE' ;
mam1 = MATE mom1 'YOUN' you 'NU' nu
                 'ALPH' alpha 'TREF' t0 'TALP' t0 ;

** Matrice de rigidite / Stifness matix
rig = RIGI mom1 mam1 ;

** Matrice de blocage / Boundary condition matrix
blm = BLOQ 'UX' 'UY' 'UZ' sut1 ;
SI ipsc ;
  OPTI 'FTRA' '6_cl_deplacement.ps' ;
FINSI ;
SI itrac ;
TRAC 'FACE' (sut1 COUL 'VIOL') are1 ;
FINSI ;

** Plusieurs manieres de representer la masse suspendue /
*  Several ways to represent the suspended mass
*  1) avec une force ponctuelle / with a single point force
ptmass = sint POIN 'PROCHE' (l (e / 2.) ((h / 2.) - r)) ;
fmass1 = FORC (0. 0. (mass1 * -9.81)) ptmass ;
vecf1  = VECT fmass1 'FORC' 'ROUG' 1.E-6 ;
SI ipsc ;
  OPTI 'FTRA' '6_forces.ps' ;
FINSI ;
SI itrac ;
TRAC vecf1 (are1 ET sint) ;
FINSI ;

*  2) avec une pression (orthogonale a la surface) /
*     with a pressure (orthogonal to the surface)
zsint  = COOR 3 sint ;
ptmass = zsint POIN 'EGINFE' (h / 2.) ;
elmass = sint ELEM 'APPU' 'STRI' ptmass ;
pres1  = mass1 * 9.81 / (MESU elmass) ;
fmass2 = PRES 'MASS' mom1 pres1 elmass ;
vecf2  = VECT fmass2 'FORC' 'VERT' 3.E-5 ;
SI itrac ;
TRAC vecf2 (are1 ET sint) ;
FINSI ;

*  3) avec une force surfacique verticale /
*     with a vertical surface force
fmass3 = FSUR 'MASS' mom1 (0. 0. (-1. * pres1)) elmass ;
vecf3  = VECT fmass3 'FORC' 'TURQ' 3.E-5 ;
SI itrac ;
TRAC vecf3 (are1 ET sint) ;
FINSI ;

SI ipsc ;
  OPTI 'FTRA' '6_cl_force.ps' ;
FINSI ;
SI itrac ;
TRAC 'FACE' (elmass COUL 'ORAN') are1 ;
FINSI ;

** Resolution des 3 cas de chargement /
*  Solving the 3 load cases
u61 u62 u63 = RESO (rig ET blm) fmass1 fmass2 fmass3 ;

** Deformees / Deformed shapes
def61 = DEFO vol1 u61 50. 'ROUG' ;
def62 = DEFO vol1 u62 50. 'VERT' ;
def63 = DEFO vol1 u63 50. 'TURQ' ;
SI ipsc ;
  OPTI 'FTRA' '6_deformees.ps' ;
FINSI ;
SI itrac ;
TRAC 'CACH' def61 ;
TRAC 'CACH' def62 ;
TRAC 'CACH' def63 ;
FINSI ;
def61a = DEFO are1 u61 50. 'ROUG' ;
def62a = DEFO are1 u62 50. 'VERT' ;
def63a = DEFO are1 u63 50. 'TURQ' ;
SI itrac ;
TRAC (def61a ET def62a ET def63a) ;
FINSI ;

** Forces de reactions / Reaction forces
rea63  = REAC u63 blm ;
vrea63 = VECT rea63 'FORC' 'LIME' ;
SI ipsc ;
  OPTI 'FTRA' '6_reactions.ps' ;
FINSI ;
SI itrac ;
TRAC vrea63 are1 ;
FINSI ;

** Deformations / Strains
eps63  = EPSI u63 mom1 'LINE' ;

** Contraintes / Stresses
sig63  = ELAS mom1 eps63 mam1 ;
SI ipsc ;
  OPTI 'FTRA' '6_contraintes.ps' ;
FINSI ;
SI itrac ;
TRAC sig63 mom1 ;
FINSI ;
def63  = DEFO vol1 u63  50. ;
SI itrac ;
TRAC sig63 mom1 def63 are1 ;
FINSI ;

** Contraintes aux points de Gauss / Stresses at Gauss points
vm63   = VMIS mom1 sig63 ;
MESS 'Mini et Maxi de von Mises : ' (MINI vm63)  (MAXI vm63) ;
SI itrac ;
TRAC vm63 mom1 ;
FINSI ;
vm63g  = CHAN 'CHPO' vm63 mom1 'SUPP' ;
SI itrac ;
TRAC vm63g ;
FINSI ;

** Valeurs le long d'un bord /
*  Values along an edge
vm63p  = CHAN 'CHPO' vm63 mom1 ;
evs1   = EVOL 'CHPO' vm63p l15 ;
SI ipsc ;
  OPTI 'FTRA' '6_evol_contraintes.ps' ;
FINSI ;
SI itrac ;
DESS evs1 ;
FINSI ;

** Valeurs le long d'une ligne quelconque /
*  Values along any line
lx     = (PROG 0.01 'PAS' 0.01 0.9) * l ;
nx     = DIME lx ;
ly     = (PROG nx * 0.) + (e / 2.) ;
lz     = (0.4 * h * (SIN (lx * 3000.))) + (h / 2.) ;
lig1   = QUEL 'SEG2' lx ly lz COUL 'ROSE' ;
SI itrac ;
TRAC (are1 ET lig1) ;
FINSI ;
vm63n  = CHAN 'NOEUD' mom1 vm63 ;
vm63l  = PROI vm63n lig1 ;
evs3   = EVOL 'ROSE' 'CHPO' vm63l lig1 ;
SI itrac ;
DESS evs3 ;
FINSI ;



*----------------------------------------------------------------------*
* Chapitre / Chapter    7.                                             *
*----------------------------------------------------------------------*
*                   C A L C U L   M E C A N I Q U E                    *
*                  elasticite + dilatation thermique                   *
*       deplacements + forces surfaciques + temperatures imposes       *
*             lineaire, quasi statique, materiau homogene              *
*                                                                      *
*               M E C H A N I C H A L   A N A L Y S I S                *
*                    elasticity + thermal expansion                    *
*        imposed displacements + surface forces + temperatures         *
*              linear, quasi static, homogeneous material              *
*----------------------------------------------------------------------*

** Increment de deformations thermiques /
*  Thermal strain increment
temp1 = tab1 . 'TEMPERATURES' . (n1 - 1) ;
epsth = EPTH temp1 mom1 mam1 ;

** Pseudo contraintes / Pseudo stresses
sigth = ELAS mom1 epsth mam1 ;

** Forces nodales eq. dilatation /
*  Eq. nodal forces thermal expansion
fth   = BSIG mom1 sigth ;

** Resolution / Solving
u7    = RESO (rig ET blm) (fmass3 ET fth) ;

** Deformee / Deformed shape
def7a = DEFO are1 u7 50. 'ORAN' ;
def0a = DEFO are1 u7 0. 'GRIS' ;
SI ipsc ;
  OPTI 'FTRA' '7_deformee.ps' ;
FINSI ;
SI itrac ;
TRAC (def0a ET def7a ET def63a) ;
FINSI ;

** Contraintes / Stresses
epstot = EPSI 'LINE' mom1 u7 ;
epsel  = epstot - epsth ;
sig7   = ELAS mom1 epsel mam1 ;
SI ipsc ;
  OPTI 'FTRA' '7_contraintes.ps' ;
FINSI ;
SI itrac ;
TRAC sig7 mom1 are1 ;
FINSI ;



*----------------------------------------------------------------------*
* Chapitre / Chapter    8.                                             *
*----------------------------------------------------------------------*
*                   C A L C U L   M E C A N I Q U E                    *
*                  elasticite + dilatation thermique                   *
*      deplacements + forces surfaciques + temperatures imposees       *
*            lineaire, quasi statique, materiau heterogene             *
*                                                                      *
*               M E C H A N I C H A L   A N A L Y S I S                *
*                    elasticity + thermal expansion                    *
*        imposed displacements + surface forces + temperatures         *
*             linear, quasi static, heterogeneous material             *
*----------------------------------------------------------------------*

** Champ alpha(x) / Field alpha(x)
x    = COOR 1 vol1 ;
xmoy = l / 2. ;
xsig = l / 5. ;
* Champ par points / Point field
chpalpha = alpha * (1. + (3. *
           (EXP (-1. * (((x - xmoy) / xsig) ** 2))))) ;
SI ipsc ;
  OPTI 'FTRA' '8_alpha_variable.ps' ;
FINSI ;
SI itrac ;
TRAC chpalpha vol1 are1 ;
FINSI ;
* Champ par elements / Element field
chealpha = CHAN 'CHAM' chpalpha mom1 ;
* Mise a jour des parmetres materiau /
* Updating the material parameters
mam1b    = MATE mom1 'YOUN' you 'NU' nu
                     'ALPH' chealpha 'TREF' t0 'TALP' t0 ;

** Mise a jour du chargement thermique / Updating the thermal load
epsth = EPTH temp1 mom1 mam1b ;
sigth = ELAS mom1 epsth mam1b ;
fth   = BSIG mom1 sigth ;

** Resolution / Solving
u8    = RESO (rig ET blm) (fmass3 ET fth) ;

** Deformee / Deformed shape
def8a = DEFO are1 u8 50. 'ROUG' ;
SI ipsc ;
  OPTI 'FTRA' '8_deformee.ps' ;
FINSI ;
SI itrac ;
TRAC (def0a ET def7a ET def8a) ;
FINSI ;

** Contraintes / Stresses
epstot = EPSI 'LINE' mom1 u8 ;
epsel  = epstot - epsth ;
sig8   = ELAS mom1 epsel mam1b ;
SI ipsc ;
  OPTI 'FTRA' '8_contraintes.ps' ;
FINSI ;
SI itrac ;
TRAC sig8 mom1 are1 ;
FINSI ;



*----------------------------------------------------------------------*
* Chapitre / Chapter    9.1                                            *
*----------------------------------------------------------------------*
*                   C A L C U L   M E C A N I Q U E                    *
*               elasto plasticite + dilatation thermique               *
*      deplacements + forces surfaciques + temperatures imposees       *
*          non lineaire, quasi statique, materiau heterogene           *
*                                                                      *
*               M E C H A N I C H A L   A N A L Y S I S                *
*                elasto plasticity + thermal expansion                 *
*        imposed displacements + surface forces + temperatures         *
*           non linear, quasi static, heterogeneous material           *
*----------------------------------------------------------------------*

** Nouveau modele mecanique / New mechanical model
mom2     = MODE vol1 'MECANIQUE' 'ELASTIQUE' 'PLASTIQUE' 'PARFAIT' ;
chealpha = CHAN 'CHAM' chpalpha mom2 ;
mam2     = MATE mom2 'YOUN' you 'NU' nu 'SIGY' sigy
                     'ALPH' chealpha 'TREF' t0 'TALP' t0 ;

** Descriptions temporelles des chargements /
*  Time descriptions of the loads
*  1) Forces imposees / Imposed forces
evm1    = EVOL 'MANU' (PROG 0. 100.) (PROG 0. 1.) ;
chamass = CHAR 'MECA' fmass3 evm1 ;
*  2) Temperatures imposees / Imposed temperatures
ttemps     = TABL ;
ttemps . 0 = 0. ;
ttemps . 1 = 100. ;
ttimp      = TABL ;
ttimp  . 0 = tab1 . 'TEMPERATURES' . 0 ;
ttimp  . 1 = tab1 . 'TEMPERATURES' . (n1 - 1) ;
chatemp    = CHAR 'T' ttemps ttimp ;

** Resolution avec la procedure PASAPAS /
*  Solving with the PASAPAS procedure
tab2                         = TABL ;
tab2 . 'MODELE'              = mom2 ;
tab2 . 'CARACTERISTIQUES'    = mam2 ;
tab2 . 'BLOCAGES_MECANIQUES' = blm ;
tab2 . 'CHARGEMENT'          = chamass ET chatemp ;
tab2 . 'TEMPS_CALCULES'      = PROG 0. 'PAS' 5. 100. ;
PASAPAS tab2 ;

** Post traitement / Post processing
* 1) Boucle : trace des contraintes a chaque instant /
*    Loop:    plotting the stresses for each time step
SI ipsc ;
  OPTI 'FTRA' '9.1_contraintes.ps' ;
FINSI ;
n2    = DIME (tab2 . 'DEPLACEMENTS') ;
liso2 = PROG 0. 'PAS' (sigy / 20.) sigy ;
REPE b2 n2 ;
  ui    = tab2 . 'DEPLACEMENTS' . (&b2 - 1) ;
  defi  = DEFO vol1 ui 50 ;
  sigi  = tab2 . 'CONTRAINTES' . (&b2 - 1) ;
  titi  = CHAI '[9.1] Contraintes au pas' ' ' (&b2 - 1) ' /' ' ' (n2 - 1) ;
  SI itrac ;
  TRAC sigi mom2 defi are1 liso2 'TITR' titi ;
  FINSI ;
FIN b2 ;

* 2) Boucle : trace des variables internes a chaque instant /
*    Loop:    plotting the internal variables for each time step
SI ipsc ;
  OPTI 'FTRA' '9.1_variables_internes.ps' ;
FINSI ;
emax  = MAXI (EXCO 'EPSE' (tab2 . 'VARIABLES_INTERNES' . (n2 - 1))) ;
liso3 = PROG 0. 'PAS' (emax / 20.) emax ;
REPE b2 n2 ;
  ui    = tab2 . 'DEPLACEMENTS' . (&b2 - 1) ;
  defi  = DEFO vol1 ui 50 ;
  epi   = tab2 . 'VARIABLES_INTERNES' . (&b2 - 1) ;
  titi  = CHAI '[9.1] Variables internes au pas' ' ' (&b2 - 1) ' /' ' ' (n2 - 1) ;
  SI itrac ;
  TRAC epi mom2 defi are1 liso3 'TITR' titi ;
  FINSI ;
FIN b2 ;



*----------------------------------------------------------------------*
* Chapitre / Chapter    9.2                                            *
*----------------------------------------------------------------------*
*                   C A L C U L   M E C A N I Q U E                    *
*               elasto plasticite + dilatation thermique               *
*      deplacements + forces surfaciques + temperatures imposees       *
*          non lineaire, quasi statique, materiau heterogene           *
*                     dependand de la temperature                      *
*                                                                      *
*               M E C H A N I C H A L   A N A L Y S I S                *
*                elasto plasticity + thermal expansion                 *
*        imposed displacements + surface forces + temperatures         *
*           non linear, quasi static, heterogeneous material           *
*                        temperature dependent                         *
*----------------------------------------------------------------------*

** Evolution de SIGY selon T /
*  Evolution of SIGY with T
evsigy = EVOL 'MANU'
 'T'     (PROG 0. 100. 150. 200. 215. 230. 235. 240. 250. 275. 300.)
 'SIGY' ((PROG 1. 0.99 0.98 0.94 0.9  0.8  0.4  0.33 0.28 0.23 0.20) * sigy) ;
SI ipsc ;
  OPTI 'FTRA' '9.2_evol_sigy.ps' ;
FINSI ;
SI itrac ;
DESS evsigy ;
FINSI ;

* Mise a jour des parmetres materiau /
* Updating the material parameters
mam2 = MATE mom2 'YOUN' you 'NU' nu 'SIGY' evsigy
                 'ALPH' chealpha 'TREF' t0 'TALP' t0 ;

** Resolution avec la procedure PASAPAS /
*  Solving with the PASAPAS procedure
tab2                         = TABL ;
tab2 . 'MODELE'              = mom2 ;
tab2 . 'CARACTERISTIQUES'    = mam2 ;
tab2 . 'BLOCAGES_MECANIQUES' = blm ;
tab2 . 'CHARGEMENT'          = chamass ET chatemp ;
tab2 . 'TEMPS_CALCULES'      = PROG 0. 'PAS' 5. 100. ;
PASAPAS tab2 ;

** Post traitement / Post processing
n2 = DIME (tab2 . 'DEPLACEMENTS') ;
* 1) Boucle : trace des variables internes a chaque instant /
*    Loop:    plotting the internal variables for each time step
SI ipsc ;
  OPTI 'FTRA' '9.2_variables_internes.ps' ;
FINSI ;
emax = MAXI (EXCO 'EPSE' (tab2 . 'VARIABLES_INTERNES' . (n2 - 1))) ;
liso3 = PROG 0. 'PAS' (emax / 20.) emax ;
REPE b2 n2 ;
  ui    = tab2 . 'DEPLACEMENTS' . (&b2 - 1) ;
  defi  = DEFO vol1 ui 50 ;
  epi   = tab2 . 'VARIABLES_INTERNES' . (&b2 - 1) ;
  titi  = CHAI '[9.2] Variables internes au pas' ' ' (&b2 - 1) ' /' ' ' (n2 - 1) ;
  SI itrac ;
    TRAC epi mom2 defi are1 liso3 'TITR' titi ;
  FINSI ;
FIN b2 ;



FIN ;
