** Options générales de calcul
OPTI 'DIME' 2 'MODE' 'PLAN' 'DEFO' 'ELEM' 'QUA8' ;
OPTI 'TRAC' 'PSC' ;

** Maillage
p0 = 0. 0. ;
p1 = 1. 0. ;
p2 = 0. 1. ;
c1 = CERC 20 p1 p0 p2 ;
p3 = 4. 0. ;
p4 = 4. 4. ;
p5 = 0. 4. ;
l1 = DROI 10 p3 p4 DROI 10 p5 ;
mail = REGL c1 l1 'DINI' 0.05 'DFIN' 0.3 ;
cmail = CONT mail ;
mpb = mail POIN 'DROITE' p0 p1 1.E-5 ;
mpg = mail POIN 'DROITE' p0 p2 1.E-5 ;
mpd = mail POIN 'DROITE' p3 p4 1.E-5 ;
lg  = cmail ELEM 'APPUYE' 'STRICTEMENT' mpg ;
ld  = cmail ELEM 'APPUYE' 'STRICTEMENT' mpd ;


** Modèle, matériau, rigidité et conditions aux limites
mo = MODE mail 'MECANIQUE' ;
ma = MATE mo 'YOUN' 20.E9 'NU' 0.3 ;
ri = RIGI mo ma ;
bl = (BLOQ 'UX' mpg) ET (BLOQ 'UY' mpb) ;
f  = PRES 'MASS' mo ld -1.E6 ;
vf = VECT f 'FORC' 'ROUG' ;
TRAC (ri ET bl) vf 'NOLE' ;

** Résolution
u = RESO (ri ET bl) f ;

** Tracé des contraintes
def1 = DEFO mail u ;
sig = SIGM mo ma u ;
OPTI 'ISOV' 'SULI' ;
TRAC sig mo def1 cmail 15 'NOLE' ;

** Evolution de la concentration de contrainte
sigp = CHAN 'CHPO' mo sig ;
evs = (EVOL 'ROUG' 'CHPO' sigp 'SMXX' lg) / 1.E6 ;
DESS evs ;

FIN ;
